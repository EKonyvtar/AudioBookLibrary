/*
 * Copyright (C) 2014 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


plugins {
    id("com.android.application")
    id("com.google.gms.google-services")

    id('com.google.firebase.crashlytics')
    id('com.google.firebase.firebase-perf')
}

android {
    compileSdkVersion 34
    // buildToolsVersion '34.0.0'

    defaultConfig {
        applicationId "com.murati.audiobook"
        minSdkVersion 22
        targetSdkVersion 33
        multiDexEnabled true

        versionCode 116
        versionName "0.50.${versionCode}"
        // TODO: Auto-increment: https://stackoverflow.com/questions/21405457/autoincrement-versioncode-with-gradle-extra-properties

        //Universal App constants
        buildConfigField 'String', 'CAST_APP_ID', '"CC1AD845"'
        buildConfigField 'String', 'APPSTORE_EMAIL', '"dev@murati.hu"'
        buildConfigField 'String', 'APPSTORE_PRIVACY_URL', '"https://ekonyvtar.github.io/audiobook-privacy_policy.html"'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    // TODO: Auto signing:
    // https://developer.android.com/studio/build/build-variants
    // System.getenv("KSTORE")

    compileOptions {
        //sourceCompatibility JavaVersion.VERSION_1_8
        //targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                'proguard-android.txt'),
                'proguard-rules.pro'
        }
    }


    flavorDimensions 'catalogue', 'language_pack', 'store'

    variantFilter { variant ->
        def catalogue = variant.getFlavors().get(0).name.toLowerCase()
        def language_pack = variant.getFlavors().get(1).name.toLowerCase()

        if (
            catalogue.contains("hu") && !language_pack.contains("hu") // Not clearly Hun
            || !catalogue.contains("hu") && language_pack.contains("hu") //Clearly multilingual
        ) {
            setIgnore(true)
        }
    }

    productFlavors {

        def addConstantTo = {target, constantName, constantValue ->
            target.manifestPlaceholders += [ (constantName):constantValue]
            target.resValue "string",  "${constantName}", "${constantValue}"
            target.buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }

        // Catalogue Definitions

        hungarian {
            dimension "catalogue"
            applicationId "com.murati.oszk.audiobook"
            versionNameSuffix "-hu"

            // Flavor specific constants
            addConstantTo(owner, "APP_NAME", "Hangoskönyvtár")
            addConstantTo(owner, "ADMOB_APP_ID", "ca-app-pub-2604838220069332~7544903903")
            addConstantTo(owner, "ADMOB_UNIT_ID_1", "ca-app-pub-2604838220069332/1672919685")

            //Multidimension constants
            addConstantTo(owner, "APPSTORE_HUAWEI_ID", "101706787")
            addConstantTo(owner, "HUAWEI_BANNER_ID","x5jdt6zn9c")
        }

        bible_hu {
            dimension "catalogue"
            applicationIdSuffix ".bible.hu"
            versionNameSuffix "-bible-hu"

            // Flavor specific constants
            addConstantTo(owner, "APP_NAME", "Biblia")
            addConstantTo(owner, "ADMOB_APP_ID", "ca-app-pub-2604838220069332~1186624167")
            addConstantTo(owner, "ADMOB_UNIT_ID_1", "ca-app-pub-2604838220069332/7627909437")

            //Multidimension constants
            addConstantTo(owner, "APPSTORE_HUAWEI_ID", "101857927")
            addConstantTo(owner, "HUAWEI_BANNER_ID","d1oieu7987")
        }

        mese_hu {
            dimension "catalogue"
            applicationIdSuffix ".mese.hu"
            versionNameSuffix "-mese-hu"

            // Flavor specific constants
            addConstantTo(owner, "APP_NAME", "Mesekönyvtár")
            //TODO: add
            addConstantTo(owner, "ADMOB_APP_ID", "ca-app-pub-2604838220069332~3156427012")
            addConstantTo(owner, "ADMOB_UNIT_ID_1", "ca-app-pub-2604838220069332/7661173454")

            //Multidimension constants
            addConstantTo(owner, "APPSTORE_HUAWEI_ID", "103673505")
            addConstantTo(owner, "HUAWEI_BANNER_ID","q84sxmouil")
        }

        french {
            dimension "catalogue"
            applicationIdSuffix ".fr"
            versionNameSuffix "-fr"

            // Flavor specific constants
            addConstantTo(owner, "APP_NAME", "Livres Audio")
            addConstantTo(owner, "ADMOB_APP_ID", "ca-app-pub-2604838220069332~5194573746")
            addConstantTo(owner, "ADMOB_UNIT_ID_1", "ca-app-pub-2604838220069332/6124512033")

            //Multidimension constants
            addConstantTo(owner, "APPSTORE_HUAWEI_ID", "101723805")
            addConstantTo(owner, "HUAWEI_BANNER_ID","j8ku8drrjy")
        }

        spanish {
            dimension "catalogue"
            applicationIdSuffix ".es"
            versionNameSuffix "-es"

            // Flavor specific constants
            addConstantTo(owner, "APP_NAME", "AudioLibros")
            addConstantTo(owner, "ADMOB_APP_ID", "ca-app-pub-2604838220069332~8227204847")
            addConstantTo(owner, "ADMOB_UNIT_ID_1", "ca-app-pub-2604838220069332/3875405609")

            //Multidimension constants
            addConstantTo(owner, "APPSTORE_HUAWEI_ID", "101720491")
            addConstantTo(owner, "HUAWEI_BANNER_ID","g9vvf24tyb")
        }

        german {
            dimension = 'catalogue'
            applicationIdSuffix = '.de'
            versionNameSuffix = '-de'

            // Flavor specific constants
            addConstantTo(owner, "APP_NAME", "Hörbücher Bibliothek")
            addConstantTo(owner, "ADMOB_APP_ID", "ca-app-pub-2604838220069332~3034430096")
            addConstantTo(owner, "ADMOB_UNIT_ID_1", "ca-app-pub-2604838220069332/8178945564")

            //Multidimension constants
            addConstantTo(owner, "APPSTORE_HUAWEI_ID", "101777037")
            addConstantTo(owner, "HUAWEI_BANNER_ID","p22w11h9q7")
        }

        // Language Packs
        LangHun {
            dimension = 'language_pack'
            resConfigs 'hu', 'en'
        }

        LangMulti {
            dimension = 'language_pack'
            resConfigs 'en', 'fr', 'es', 'de'
        }


        // Store definitions
        GooglePlay {
            dimension = 'store'
            versionNameSuffix = '-google'
            buildConfigField 'String', 'APPSTORE_URL', '"http://play.google.com/store/apps/details?id=%s"'

            dependencies {
                implementation 'com.google.android.gms:play-services-ads:20.6.0'

            }
        }

        HuaweiAppGallery {
            dimension = 'store'
            versionNameSuffix = '-huawei'
            buildConfigField 'String', 'APPSTORE_URL', '"https://appgallery.cloud.huawei.com/marketshare/app/C%s"'

            dependencies {
                //implementation 'com.huawei.hms:ads-lite:13.4.53.300'
                //implementation 'com.huawei.hms:base:5.0.3.300'
            }
        }

        //TODO: define paid and free dimensions
    }
    lint {
        abortOnError false
    }
    namespace 'com.murati.audiobook'
}

repositories {
    //flatDir {
    //    dirs 'libs'
    //}
    google()
    jcenter()
    maven { url 'https://developer.huawei.com/repo/' }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    compileOnly 'com.google.android.wearable:wearable:2.9.0'
    implementation 'com.google.android.support:wearable:2.9.0'

    implementation 'com.google.android.gms:play-services-cast-framework:21.3.0'

    implementation("androidx.core:core:1.9.0")
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    // 1.4.1 - The minCompileSdk (31) specified in a
    //     dependency's AAR metadata (META-INF/com/android/build/gradle/aar-metadata.properties)
    //     is greater than this module's compileSdkVersion (android-30).
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.mediarouter:mediarouter:1.4.0'
    implementation 'androidx.leanback:leanback:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'

    //Exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.8.4'

    //Graphical libraries
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // Add the dependencies for any other desired Firebase products
    // https://firebase.google.com/docs/android/setup#available-libraries
    implementation(platform("com.google.firebase:firebase-bom:32.1.1"))
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-config'
    implementation("com.google.firebase:firebase-analytics")
    implementation 'com.google.firebase:firebase-perf'


    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.15.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.annotation:annotation:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}
